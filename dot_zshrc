# 1. 非インタラクティブシェルのチェック
[[ $- != *i* ]] && return

# 2. XDG環境変数
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"

# 3. キャッシュディレクトリ
CACHE_DIR="${XDG_CACHE_HOME}/zsh"
mkdir -p "$CACHE_DIR"

# 4. Instant Prompt (P10k)
if [[ -r "${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# 5. Homebrew検出とPATH設定
find_brew() {
  local paths=("/opt/homebrew" "/usr/local" "$HOME/.linuxbrew")
  for p in $paths; do
    [[ -x "$p/bin/brew" ]] && { echo "$p"; return 0; }
  done
  return 1
}
BREW_PREFIX="${HOMEBREW_PREFIX:-$(find_brew 2>/dev/null)}"

typeset -U path
path=(
  "$HOME/.local/bin"(N-/)
  "$BREW_PREFIX/bin"(N-/)
  $path
)

# 6. History設定
HISTFILE="$CACHE_DIR/.zsh_history"
HISTSIZE=100000
SAVEHIST=100000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt SHARE_HISTORY
setopt HIST_VERIFY

# 7. Zshオプション
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt CORRECT
setopt INTERACTIVE_COMMENTS  # 追加推奨
setopt AUTO_CD               # 追加推奨

# 8. Completions
typeset -U fpath
[[ -d "$BREW_PREFIX/share/zsh-completions" ]] && fpath=("$BREW_PREFIX/share/zsh-completions" $fpath)

autoload -Uz compinit
dump="$CACHE_DIR/.zcompdump-${ZSH_VERSION}"
if [[ -f "$dump.zwc" ]] && [[ "$dump.zwc" -nt "$dump" ]]; then
  compinit -C -d "$dump"
else
  compinit -d "$dump"
  [[ -f "$dump" ]] && zcompile "$dump"
fi

# 9. Powerlevel10k
[[ -r "$BREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme" ]] && \
  source "$BREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme"
[[ -f "$HOME/.p10k.zsh" ]] && source "$HOME/.p10k.zsh"

# 10. エディタ設定
export EDITOR="nvim"
export VISUAL="nvim"

# zsh-autosuggestions
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[unknown-token]='fg=#d8dee9'
ZSH_HIGHLIGHT_STYLES[unknown-command]='fg=#d8dee9'
ZSH_AUTOSUGGEST_STRATEGY=(history)
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=2000
[[ -r "$BREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh" ]] && source "$BREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh"

# fzf
typeset -a FZF_BASE_OPTS
typeset -a FZF_FILE_PREVIEW_OPTS
typeset -a _ZO_OPTS
typeset -g NBSP=$'\u00A0'
FZF_FILE_PREVIEW_OPTS=(
  "--preview"
  "'bat --style=numbers,changes --color=always --paging=never {}'"
)
FZF_BASE_OPTS=(
  "--color=fg:#6b7994,bg:#2e3440,hl:#b48ead"
  "--color=fg+:#d8dee9,bg+:#2e3440,hl+:#b48ead"
  "--color=info:#8fbcbb,prompt:#8fbcbb,pointer:#8fbcbb"
  "--color=marker:#8fbcbb,spinner:#5e81ac,header:#81a1c1"
  "--color=border:#4c566a"
  "--border=rounded"
  "--info=default"
  "--height=75%"
  "--layout=reverse"
  "--pointer=❯"
  "--no-separator"
  "--no-scrollbar"
  "--prompt=❯${NBSP}"
)
export FZF_DEFAULT_OPTS="${(j: :)FZF_BASE_OPTS}"

_ZO_OPTS=(
  "--no-sort"
  "--height=75%"
  "--reverse"
  "--exit-0"
  "--select-1"
  "--color=fg:#6b7994,bg:#2e3440,hl:#b48ead"
  "--color=fg+:#d8dee9,bg+:#2e3440,hl+:#b48ead"
  "--color=info:#8fbcbb,prompt:#8fbcbb,pointer:#8fbcbb"
  "--color=marker:#8fbcbb,spinner:#5e81ac,header:#81a1c1"
  "--color=border:#4c566a"
  "--border=rounded"
  "--info=default"
  "--no-separator"
  "--no-scrollbar"
  "--pointer=❯"
  "--prompt=❯${NBSP}"
  # "--preview"
  # "'([[ -e {2..}/README.md ]] && bat --color=always --style=numbers --line-range=:50 {2..}/README.md) || eza -alo --time-style iso --color=always --group-directories-first -- {2..}'"
  # "eza -alo --time-style iso --color=always --group-directories-first -- {2..}"
  "--preview='eza -alo \
    --time-style=iso --color=always --group-directories-first -- {2..}'"
  "--preview-window=wrap"
)
export _ZO_FZF_OPTS="${(j: :)_ZO_OPTS}"

if command -v fd >/dev/null 2>&1; then
  export FZF_DEFAULT_COMMAND='fd --hidden --follow --exclude .git --exclude node_modules --type f'
  export FZF_ALT_C_COMMAND='fd --hidden --follow --exclude .git --exclude node_modules --type d'
fi

# alias
alias ..='cd ..'
alias vpn="$HOME/Documents/shellscripts/vpn/vpn.sh"
alias vim='nvim'

# function
autoload -Uz add-zsh-hook
after_prompt_once() {
  command -v mise   >/dev/null 2>&1 && eval "$(command mise activate zsh)"
  command -v zoxide >/dev/null 2>&1 && eval "$(command zoxide init zsh)"
  add-zsh-hook -d precmd after_prompt_once
}
add-zsh-hook precmd after_prompt_once

cd_ls() {
  [[ $PWD -ef $HOME ]] && return
  [[ $PWD =~ '/(node_modules|vendor|\.venv|venv|dist|build|\.cache|\.next|target|\.terraform|\.git)' ]] && return
  command eza -al --color=never -o --time-style=long-iso -F always
}
add-zsh-hook chpwd cd_ls

fzf_history() {
  zle -I
  local sel
  sel=$(
    history -n -r 1 \
    | awk '!seen[$0]++' \
    | FZF_DEFAULT_OPTS="${(j: :)FZF_BASE_OPTS} --prompt=❯${NBSP}" fzf --query "$LBUFFER"
  )
  if [[ $? -ne 0 || -z $sel ]]; then
    zle -R; return 0
  fi
  BUFFER=$sel
  CURSOR=$#BUFFER
  zle redisplay
}
zle -N fzf_history
bindkey '^r' fzf_history

fzf_find() {
  setopt localoptions pipefail
  zle -I
  local fzf_env file
  fzf_env="${(j: :)FZF_BASE_OPTS} ${(j: :)FZF_FILE_PREVIEW_OPTS}"

  if command -v fd >/dev/null 2>&1; then
    file=$(
      fd --hidden --follow --exclude .git --exclude node_modules --type f \
      | FZF_DEFAULT_OPTS="$fzf_env" fzf
    )
  else
    file=$(
      command find . -type d \( -name .git -o -name node_modules \) -prune -o -type f -print \
      | FZF_DEFAULT_OPTS="$fzf_env" fzf
    )
  fi
  if [[ $? -ne 0 || -z $file ]]; then
    zle -R; return 0
  fi
  BUFFER="nvim ${(q)file}"
  CURSOR=$#BUFFER
  zle accept-line
}
zle -N fzf_find
bindkey '^f' fzf_find

# zsh-syntax-highlighting
if [[ -r "$BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]]; then
  source "$BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
fi

#  .zshrcのコンパイル（次回起動時の高速化）
() {
  local zshrc="$HOME/.zshrc"
  local p10k="$HOME/.p10k.zsh"
  local zwc="$zshrc.zwc"

  if [[ -f "$zshrc" && ( ! -f "$zwc" || "$zshrc" -nt "$zwc" ) ]]; then
    zcompile "$zshrc"
  fi
  if [[ -f "$p10k" && ( ! -f "$p10k.zwc" || "$p10k" -nt "$p10k.zwc" ) ]]; then
    zcompile "$p10k"
  fi
}
