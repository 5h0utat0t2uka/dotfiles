if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Homebrew
BREW_PREFIX="${HOMEBREW_PREFIX:-/opt/homebrew}"
if [[ ! -d "$BREW_PREFIX" ]] && command -v brew >/dev/null 2>&1; then
  BREW_PREFIX="$(brew --prefix)"
fi
[[ -d "$BREW_PREFIX" ]] || BREW_PREFIX=""

# Powerlevel10k
if [[ -r "$BREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme" ]]; then
  source "$BREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme"
fi
[[ -f "$HOME/.p10k.zsh" ]] && source "$HOME/.p10k.zsh"

# zsh-completions
typeset -U fpath
if [[ -d "$BREW_PREFIX/share/zsh-completions" ]]; then
  fpath=("$BREW_PREFIX/share/zsh-completions" $fpath)
fi
CACHE_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"
mkdir -p "$CACHE_DIR"
autoload -Uz compinit
compinit -d "$CACHE_DIR/.zcompdump-${ZSH_VERSION}"

# zsh-autosuggestions
if [[ -r "$BREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh" ]]; then
  source "$BREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
fi

# fzf
typeset -a FZF_OPTIONS
typeset -g NBSP=$'\u00A0'
FZF_OPTIONS=(
  "--color=fg:#d8dee9,bg:#2e3440,hl:#8fbcbb"
  "--color=fg+:#d8dee9,bg+:#2e3440,hl+:#8fbcbb"
  "--color=info:#8fbcbb,prompt:#8fbcbb,pointer:#8fbcbb"
  "--color=marker:#8fbcbb,spinner:#5e81ac,header:#81a1c1"
  "--color=border:#4c566a"
  "--border=rounded"
  "--height=50%"
  "--layout=reverse"
  "--header="
  "--pointer=❯"
  "--no-scrollbar"
)
export FZF_DEFAULT_OPTS="${(j: :)FZF_OPTIONS} --prompt=❯${NBSP} --preview 'bat --style=numbers,changes --color=always --paging=never {}'"

if command -v fd >/dev/null 2>&1; then
  export FZF_DEFAULT_COMMAND='fd --hidden --follow --exclude .git --exclude node_modules --type f'
  export FZF_ALT_C_COMMAND='fd --hidden --follow --exclude .git --exclude node_modules --type d'
fi

# alias
alias vpn="$HOME/Documents/shellscripts/vpn/vpn.sh"
alias vim='nvim'
export EDITOR="nvim"
export VISUAL="nvim"

# function
cd_ls() {
  if [[ $PWD -ef $HOME ]]; then
    return
  fi
  local exclude=(node_modules vendor .venv venv dist build .cache .next target .terraform .git)
  local pattern
  for pattern in "${exclude[@]}"; do
    [[ $PWD == *"/$pattern"* ]] && return
  done
  ls -alG
}
autoload -Uz add-zsh-hook
add-zsh-hook chpwd cd_ls

# zsh-syntax-highlighting
if [[ -r "$BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]]; then
  source "$BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
fi
